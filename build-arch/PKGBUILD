# Maintainer: shmilee <echo c2htaWxlZS56anVAZ21haWwuY29tCg==|base64 -d>

_gitname="rst2arb"
pkgname=rst2arb
pkgver=0.16.a2fabbc
pkgrel=1
pkgdesc="Convert ReST to article, report and beamer."
license=('GPL')
arch=(any)
depends=('pandoc' 'python')
optdepends=('texlive-langchinese: for Chinese texts support'
            'texlive-langjapanese: for Japanese texts support'
            'texlive-langkorean: for  Korean texts support')
makedepends=('git' 'texlive-core')
url='https://github.com/shmilee/rst2arb'
source=("git://github.com/shmilee/${_gitname}.git"
        "git+https://github.com/jrnold/beamercolorthemesolarized.git"
        "git+https://github.com/matze/mtheme.git")
install=texlive.install
md5sums=('SKIP' 'SKIP' 'SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  echo -n "0.$(git rev-list --count HEAD)."
  git describe --always|sed 's|-|.|g'
}
prepare() {
  cd "$srcdir/$_gitname"
  git submodule init
  git config submodule.latex/beamercolorthemesolarized.url \
      "$srcdir/beamercolorthemesolarized"
  git config submodule.latex/beamerthemem.url \
      "$srcdir/mtheme"
}

build() {
  cd "$srcdir/$_gitname"
  make pre
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
}
